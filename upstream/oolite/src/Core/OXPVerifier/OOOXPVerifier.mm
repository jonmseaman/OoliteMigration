/*

OOOXPVerifier.m


Copyright (C) 2007-2013 Jens Ayton and contributors

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

/*	Design notes:
	see "verifier design.txt".
*/

#import "OOOXPVerifier.h"
#import <objc/runtime.h>
#import <objc/objc-arc.h>

#if OO_OXP_VERIFIER_ENABLED

#if OOLITE_WINDOWS

#ifdef __OBJC__
#pragma push_macro("interface")
#undef interface
#define interface struct
#endif

#include <shlwapi.h>

#ifdef __OBJC__
#pragma pop_macro("interface")
#endif

#include <tchar.h>
#endif

#import "OOOXPVerifierStageInternal.h"
#import "OOLoggingExtended.h"
#import "ResourceManager.h"
#import "OOPListView.h"
#import "GameController.h"
#import "OOCacheManager.h"
#import "OODebugStandards.h"
#include "oofnd/FileSystem.hpp"
#include "oofnd/Process.hpp"
#include "oofnd/StdLib.hpp"

static void SwitchLogFile(NSString *name);
static void NoteVerificationStage(NSString *displayName, NSString *stage);
static void OpenLogFile(NSString *name);

@interface OOOXPVerifier (OOPrivate)

- (id)initWithPath:(NSString *)path;
- (void)run;

- (void)setUpLogOverrides;

- (void)registerBaseStages;
- (void)buildDependencyGraph;
- (void)runStages;

- (BOOL)setUpDependencies:(NSSet *)dependencies
				 forStage:(OOOXPVerifierStage *)stage;

- (void)setUpDependents:(NSSet *)dependents
			   forStage:(OOOXPVerifierStage *)stage;

- (void)dumpDebugGraphviz;

@end


@implementation OOOXPVerifier

/**
 * \ingroup cli
 * Scans the command line for -verify-oxp or --verify-oxp, followed by a
 * path to the oxp to verify.
 *
 * @return YES or NO
 */
+ (BOOL)runVerificationIfRequested
{
	NSString			*foundPath = nil;
	BOOL				exists, isDirectory;
	OOOXPVerifier		*verifier = nil;
	void				*pool = NULL;
	
	pool = objc_autoreleasePoolPush();
	
	const std::vector<std::string> &arguments = oo::process::arguments();

	// Scan for -verify-oxp or --verify-oxp followed by relative path
	for (size_t argIndex = 0; argIndex < arguments.size(); argIndex++)
	{
		const std::string &arg = arguments[argIndex];
		if (arg == "-verify-oxp" || arg == "--verify-oxp")
		{
			if (argIndex + 1 < arguments.size())  foundPath = [NSString stringWithUTF8String:arguments[argIndex + 1].c_str()];
			if (foundPath == nil)
			{
				OOLog(@"verifyOXP.noPath", @"***** ERROR: %s passed without path argument; nothing to verify.", arg.c_str());
				objc_autoreleasePoolPop(pool);
				return YES;
			}
			foundPath = [foundPath stringByExpandingTildeInPath];
			break;
		}
	}
	
	if (foundPath == nil)
	{
		objc_autoreleasePoolPop(pool);
		return NO;
	}
	
	// We got a path; does it point to a directory?
	oo::fs::FileType foundType = oo::fs::fileType(oo::fs::pathFromUTF8([foundPath UTF8String]));
	exists = (foundType != oo::fs::FileType::none);
	isDirectory = (foundType == oo::fs::FileType::directory);
	if (!exists)
	{
		OOLog(@"verifyOXP.badPath", @"***** ERROR: no OXP exists at path \"%@\"; nothing to verify.", foundPath);
	}
	else if (!isDirectory)
	{
		OOLog(@"verifyOXP.badPath", @"***** ERROR: \"%@\" is a file, not an OXP directory; nothing to verify.", foundPath);
	}
	else
	{
		verifier = [[OOOXPVerifier alloc] initWithPath:foundPath];
		objc_autoreleasePoolPop(pool);
		pool = objc_autoreleasePoolPush();
		[verifier run];
		[verifier release];
	}
	objc_autoreleasePoolPop(pool);
	
	// Whether or not we got a valid path, -verify-oxp was passed.
	return YES;
}


- (void)dealloc
{
	[_verifierPList release];
	[_basePath release];
	[_displayName release];
	[_stagesByName release];
	[_waitingStages release];
	
	[super dealloc];
}


- (void)registerStage:(OOOXPVerifierStage *)stage
{
	NSString				*name = nil;
	OOOXPVerifierStage		*existing = nil;
	
	// Sanity checking
	if (stage == nil)  return;
	
	if (![stage isKindOfClass:[OOOXPVerifierStage class]])
	{
		OOLog(@"verifyOXP.registration.failed", @"Attempt to register class %@ as a verifier stage, but it is not a subclass of OOOXPVerifierStage; ignoring.", [stage class]);
		return;
	}
	
	if (!_openForRegistration)
	{
		OOLog(@"verifyOXP.registration.failed", @"Attempt to register verifier stage %@ after registration closed, ignoring.", stage);
		return;
	}
	
	name = [stage name];
	if (name == nil)
	{
		OOLog(@"verifyOXP.registration.failed", @"Attempt to register verifier stage %@ with nil name, ignoring.", stage);
		return;
	}
		
	// We can only have one stage with a given name. Registering the same stage twice is OK, though.
	existing = [_stagesByName objectForKey:name];
	if (existing == stage)  return;
	if (existing != nil)
	{
		OOLog(@"verifyOXP.registration.failed", @"Attempt to register verifier stage %@ with same name as stage %@, ignoring.", stage, existing);
		return;
	}
	
	// Checks passed, store state.
	[stage setVerifier:self];
	[_stagesByName setObject:stage forKey:name];
	[_waitingStages addObject:stage];
}


- (NSString *)oxpPath
{
	return [[_basePath retain] autorelease];
}


- (NSString *)oxpDisplayName
{
	return [[_displayName retain] autorelease];
}


- (id)stageWithName:(NSString *)name
{
	if (name == nil)  return nil;
	
	return [_stagesByName objectForKey:name];
}


- (id)configurationValueForKey:(NSString *)key
{
	return [_verifierPList objectForKey:key];
}


- (NSArray *)configurationArrayForKey:(NSString *)key
{
	return oo::PListView(_verifierPList).get<NSArray *>(key);
}


- (NSDictionary *)configurationDictionaryForKey:(NSString *)key
{
	return oo::PListView(_verifierPList).get<NSDictionary *>(key);
}


- (NSString *)configurationStringForKey:(NSString *)key
{
	return oo::PListView(_verifierPList).get<NSString *>(key);
}


- (NSSet *)configurationSetForKey:(NSString *)key
{
	NSArray *array = oo::PListView(_verifierPList).get<NSArray *>(key);
	return array != nil ? [NSSet setWithArray:array] : nil;
}

@end


@implementation OOOXPVerifier (OOPrivate)

- (id)initWithPath:(NSString *)path
{
	self = [super init];

	OOSetStandardsForOXPVerifierMode();

	NSString *verifierPListPath = [[[ResourceManager builtInPath] stringByAppendingPathComponent:@"Config"] stringByAppendingPathComponent:@"verifyOXP.plist"];
	_verifierPList = [[NSDictionary dictionaryWithContentsOfFile:verifierPListPath] retain];
	
	_basePath = [path copy];
	_displayName = [_basePath lastPathComponent];	// what GNUstep's -displayNameAtPath: returns
	if (_displayName == nil)  _displayName = [_basePath lastPathComponent];
	[_displayName retain];
	
	_stagesByName = [[NSMutableDictionary alloc] init];
	_waitingStages = [[NSMutableSet alloc] init];
	
	if (_verifierPList == nil ||
		_basePath == nil)
	{
		OOLog(@"verifyOXP.setup.failed", @"%@", @"***** ERROR: failed to set up OXP verifier.");
		[self release];
		return nil;
	}
	
	_openForRegistration = YES;
	
	return self;
}


- (void)run
{
	NoteVerificationStage(_displayName, @"");
	
	[self setUpLogOverrides];
	
	/*	We need to be able to look up internal files, but not other OXP files.
		To do this without clobbering the disk cache, we disable cache writes.
	*/
	[[OOCacheManager sharedCache] flush];
	[[OOCacheManager sharedCache] setAllowCacheWrites:NO];
	/* FIXME: the OXP verifier should load files from OXPs which have
	 * been explicitly listed as required_oxps in the
	 * manifest. Reading the manifest from the OXP being verified and
	 * setting 'id:<its identifier>' below will do this. */
	[ResourceManager setUseAddOns:SCENARIO_OXP_DEFINITION_NONE];
	
	SwitchLogFile(_displayName);
	OOLog(@"verifyOXP.start", @"Running OXP verifier for %@", _basePath);//_displayName);
	
	[self registerBaseStages];
	[self buildDependencyGraph];
	[self runStages];
	
	NoteVerificationStage(_displayName, @"");
	OOLog(@"verifyOXP.done", @"%@", @"OXP verification complete.");
	
	OpenLogFile(_displayName);
}


- (void)setUpLogOverrides
{
	NSDictionary			*overrides = nil;
	NSString				*messageClass = nil;
	id						verbose = nil;
	
	OOLogSetShowMessageClassTemporary(oo::PListView(_verifierPList).get<BOOL>(@"logShowMessageClassOverride", NO));
	
	overrides = oo::PListView(_verifierPList).get<NSDictionary *>(@"logControlOverride");
	foreachkey (messageClass, overrides)
	{
		OOLogSetDisplayMessagesInClass(messageClass, oo::PListView(overrides).get<BOOL>(messageClass, NO));
	}
	
	/*	Since actually editing logControlOverride is a pain, we also allow
		overriding verifyOXP.verbose through user defaults. This is at least
		as much a pain under GNUstep, but very convenient under OS X.
	*/
	verbose = [[NSUserDefaults standardUserDefaults] objectForKey:@"oxp-verifier-verbose-logging"];
	if (verbose != nil)  OOLogSetDisplayMessagesInClass(@"verifyOXP.verbose", OOBooleanFromObject(verbose, NO));
}


- (void)registerBaseStages
{
	NSSet					*stages = nil;
	NSSet					*excludeStages = nil;
	NSString				*stageName = nil;
	Class					stageClass = Nil;
	OOOXPVerifierStage		*stage = nil;
	
	@autoreleasepool
	{
		// Load stages specified as array of class names in verifyOXP.plist
		stages = [self configurationSetForKey:@"stages"];
		excludeStages = [self configurationSetForKey:@"excludeStages"];
		if ([excludeStages count] != 0)
		{
			stages = [[stages mutableCopy] autorelease];
			[(NSMutableSet *)stages minusSet:excludeStages];
		}
		foreach (stageName, stages)
		{
			if ([stageName isKindOfClass:[NSString class]])
			{
				stageClass = NSClassFromString(stageName);
				if (stageClass == Nil)
				{
					OOLog(@"verifyOXP.registration.failed", @"Attempt to register unknown class %@ as a verifier stage, ignoring.", stageName);
					continue;
				}
				stage = [[stageClass alloc] init];
				[self registerStage:stage];
				[stage release];
			}
		}
	}
}


- (void)buildDependencyGraph
{
	NSArray					*stageKeys = nil;
	NSString				*stageKey = nil;
	OOOXPVerifierStage		*stage = nil;
	NSString				*name = nil;
	NSSet					*dependencies = nil,
							*dependents = nil;
	
	@autoreleasepool
	{
		/*	Iterate over all stages, getting dependency and dependent sets.
			This is done in advance so that -dependencies and -dependents may
			register stages.
			Keyed by stage, not retaining it; were NSMutableDictionaries keyed by
			valueWithNonretainedObject: (bead oo-3rb.50). Only looked up, never
			iterated. The sets are retained and autoreleased into this pool, which
			is when the autoreleased dictionaries released them.
		*/
		std::unordered_map<OOOXPVerifierStage *, NSSet *> dependenciesByStage;
		std::unordered_map<OOOXPVerifierStage *, NSSet *> dependentsByStage;
		
		for (;;)
		{
			/*	Loop while there are stages whose dependency lists haven't been
				checked. This is an indeterminate loop since new ones can be
				added.
			*/
			stage = [_waitingStages anyObject];
			if (stage == nil)  break;
			[_waitingStages removeObject:stage];
			
			dependencies = [stage dependencies];
			if (dependencies != nil)
			{
				dependenciesByStage[stage] = [[dependencies retain] autorelease];
			}
			
			dependents = [stage dependents];
			if (dependents != nil)
			{
				dependentsByStage[stage] = [[dependents retain] autorelease];
			}
		}
		[_waitingStages release];
		_waitingStages = nil;
		_openForRegistration = NO;
		
		// Iterate over all stages, resolving dependencies.
		stageKeys = [_stagesByName allKeys];	// Get the keys up front because we may need to remove entries from dictionary.
		
		foreach (stageKey, stageKeys)
		{
			stage = [_stagesByName objectForKey:stageKey];
			if (stage == nil)  continue;
			
			// Sanity check
			name = [stage name];
			if (![stageKey isEqualToString:name])
			{
				OOLog(@"verifyOXP.buildDependencyGraph.badName", @"***** Stage name appears to have changed from \"%@\" to \"%@\" for verifier stage %@, removing.", stageKey, name, stage);
				[_stagesByName removeObjectForKey:stageKey];
				continue;
			}
			
			// Get dependency set
			auto foundDependencies = dependenciesByStage.find(stage);
			dependencies = (foundDependencies != dependenciesByStage.end()) ? foundDependencies->second : nil;
			
			if (dependencies != nil && ![self setUpDependencies:dependencies forStage:stage])
			{
				[_stagesByName removeObjectForKey:stageKey];
			}
		}
		
		/*	Iterate over all stages again, resolving reverse dependencies.
			This is done in a separate pass because reverse dependencies are "weak"
			while forward dependencies are "strong". 
		*/
		stageKeys = [_stagesByName allKeys];
		
		foreach (stageKey, stageKeys)
		{
			stage = [_stagesByName objectForKey:stageKey];
			if (stage == nil)  continue;
			
			// Get dependent set
			auto foundDependents = dependentsByStage.find(stage);
			dependents = (foundDependents != dependentsByStage.end()) ? foundDependents->second : nil;
			
			if (dependents != nil)
			{
				[self setUpDependents:dependents forStage:stage];
			}
		}
		
		_waitingStages = [[NSMutableSet alloc] initWithArray:[_stagesByName allValues]];
		[_waitingStages makeObjectsPerformSelector:@selector(dependencyRegistrationComplete)];
		
		if ([[NSUserDefaults standardUserDefaults] boolForKey:@"oxp-verifier-dump-debug-graphviz"])
		{
			[self dumpDebugGraphviz];
		}
	}
}


- (void)runStages
{
	void					*pool = NULL;
	OOOXPVerifierStage		*candidateStage = nil,
							*stageToRun = nil;
	NSString				*stageName = nil;
	
	// Loop while there are still stages to run.
	for (;;)
	{
		pool = objc_autoreleasePoolPush();
		
		// Look through queue for a stage that's ready
		stageToRun = nil;
		foreach (candidateStage, _waitingStages)
		{
			if ([candidateStage canRun])
			{
				stageToRun = candidateStage;
				break;
			}
		}
		if (stageToRun == nil)
		{
			// No more runnable stages
			objc_autoreleasePoolPop(pool);
			break;
		}
		
		stageName = nil;
		OOLogPushIndent();
		@try
		{
			stageName = [stageToRun name];
			if ([stageToRun shouldRun])
			{
				NoteVerificationStage(_displayName, stageName);
				OOLog(@"verifyOXP.runStage", @"%@", stageName);
				OOLogIndent();
				[stageToRun performRun];
			}
			else
			{
				OOLog(@"verifyOXP.verbose.skipStage", @"- Skipping stage: %@ (nothing to do).", stageName);
				[stageToRun noteSkipped];
			}
		}
		@catch (NSException *exception)
		{
			if (stageName == nil)  stageName = [[stageToRun class] description];
			OOLog(@"verifyOXP.exception", @"***** Exception occurred when running OXP verifier stage \"%@\": %@: %@", stageName, [exception name], [exception reason]);
		}
		OOLogPopIndent();
		
		[_waitingStages removeObject:stageToRun];
		objc_autoreleasePoolPop(pool);
	}
	
	pool = objc_autoreleasePoolPush();
	
	if ([_waitingStages count] != 0)
	{
		OOLog(@"verifyOXP.incomplete", @"%@", @"Some verifier stages could not be run:");
		OOLogIndent();
		foreach (candidateStage, _waitingStages)
		{
			OOLog(@"verifyOXP.incomplete.item", @"%@", candidateStage);
		}
		OOLogOutdent();
	}
	[_waitingStages release];
	_waitingStages = nil;
	
	objc_autoreleasePoolPop(pool);
}


- (BOOL)setUpDependencies:(NSSet *)dependencies
				 forStage:(OOOXPVerifierStage *)stage
{
	NSString				*depName = nil;
	OOOXPVerifierStage		*depStage = nil;
	
	// Iterate over dependencies, connecting them up.
	foreach (depName, dependencies)
	{
		depStage = [_stagesByName objectForKey:depName];
		if (depStage == nil)
		{
			OOLog(@"verifyOXP.buildDependencyGraph.unresolved", @"Verifier stage %@ has unresolved dependency \"%@\", skipping.", stage, depName);
			return NO;
		}
		
		if ([depStage isDependentOf:stage])
		{
			OOLog(@"verifyOXP.buildDependencyGraph.circularReference", @"Verifier stages %@ and %@ have a dependency loop, skipping.", stage, depStage);
			[_stagesByName removeObjectForKey:depName];
			return NO;
		}
		
		[stage registerDependency:depStage];
	}
	
	return YES;
}


- (void)setUpDependents:(NSSet *)dependents
			   forStage:(OOOXPVerifierStage *)stage
{
	NSString				*depName = nil;
	OOOXPVerifierStage		*depStage = nil;
	
	// Iterate over dependents, connecting them up.
	foreach (depName, dependents)
	{
		depStage = [_stagesByName objectForKey:depName];
		if (depStage == nil)
		{
			OOLog(@"verifyOXP.buildDependencyGraph.unresolved", @"Verifier stage %@ has unresolved dependent \"%@\".", stage, depName);
			continue;	// Unresolved/conflicting dependents are non-fatal
		}
		
		if ([stage isDependentOf:depStage])
		{
			OOLog(@"verifyOXP.buildDependencyGraph.circularReference", @"Verifier stage %@ lists %@ as both dependent and dependency (possibly indirectly); will execute %@ after %@.", stage, depStage, stage, depStage);
			continue;
		}
		
		[depStage registerDependency:stage];
	}
}


- (void)dumpDebugGraphviz
{
	NSMutableString				*graphViz = nil;
	NSDictionary				*graphVizTemplate = nil;
	NSString					*arcTemplate = nil,
								*startTemplate = nil,
								*endTemplate = nil;
	OOOXPVerifierStage			*stage = nil;
	NSSet						*deps = nil;
	OOOXPVerifierStage			*dep = nil;
	
	graphVizTemplate = [self configurationDictionaryForKey:@"debugGraphvizTempate"];
	graphViz = [NSMutableString stringWithFormat:oo::PListView(graphVizTemplate).get<NSString *>(@"preamble"), [NSDate date]];
	
	/*	Pass 1: enumerate over graph setting node attributes for each stage.
		We use pointers as node names for simplicity of generation.
	*/
	arcTemplate = oo::PListView(graphVizTemplate).get<NSString *>(@"node");
	foreach (stage, [_stagesByName allValues])
	{
		[graphViz appendFormat:arcTemplate, stage, [stage class], [stage name]];
	}
	
	[graphViz appendString:oo::PListView(graphVizTemplate).get<NSString *>(@"forwardPreamble")];
	
	/*	Pass 2: enumerate over graph setting forward arcs for each dependency.
	*/
	arcTemplate = oo::PListView(graphVizTemplate).get<NSString *>(@"forwardArc");
	startTemplate = oo::PListView(graphVizTemplate).get<NSString *>(@"startArc");
	foreach (stage, [_stagesByName allValues])
	{
		deps = [stage resolvedDependencies];
		if ([deps count] != 0)
		{
			foreach (dep, deps)
			{
				[graphViz appendFormat:arcTemplate, dep, stage];
			}
		}
		else
		{
			[graphViz appendFormat:startTemplate, stage];
		}
	}
	
	[graphViz appendString:oo::PListView(graphVizTemplate).get<NSString *>(@"backwardPreamble")];
	
	/*	Pass 3: enumerate over graph setting backward arcs for each dependent.
	*/
	arcTemplate = oo::PListView(graphVizTemplate).get<NSString *>(@"backwardArc");
	endTemplate = oo::PListView(graphVizTemplate).get<NSString *>(@"endArc");
	foreach (stage, [_stagesByName allValues])
	{
		deps = [stage resolvedDependents];
		if ([deps count] != 0)
		{
			foreach (dep, deps)
			{
				[graphViz appendFormat:arcTemplate, dep, stage];
			}
		}
		else
		{
			[graphViz appendFormat:endTemplate, stage];
		}
	}
	
	[graphViz appendString:oo::PListView(graphVizTemplate).get<NSString *>(@"postamble")];
	
	// Write file
	[ResourceManager writeDiagnosticString:graphViz toFileNamed:@"OXPVerifierStageDependencies.dot"];
}

@end


#import "OOLogOutputHandler.h"


static void SwitchLogFile(NSString *name)
{
//#ifndef OOLITE_LINUX
	name = [name stringByAppendingPathExtension:@"log"];
	OOLog(@"verifyOXP.switchingLog", @"Switching log files -- logging to \"%@\".", name);
	OOLogOutputHandlerChangeLogFile(name);
//#else
//	OOLog(@"verifyOXP.switchingLog", @"Switching logging to <stdout>.");
//	OOLogOutputHandlerStartLoggingToStdout();
//#endif
}


static void NoteVerificationStage(NSString *displayName, NSString *stage)
{
	[[GameController sharedController] logProgress:[NSString stringWithFormat:@"Verifying %@\n%@", displayName, stage]];
}


static void OpenLogFile(NSString *name)
{
	//	Open log file in appropriate application / provide feedback.
	
	if (oo::PListView([NSUserDefaults standardUserDefaults]).get<BOOL>(@"oxp-verifier-open-log", YES))
	{
#if OOLITE_MAC_OS_X
		[[NSWorkspace sharedWorkspace] openFile:OOLogHandlerGetLogPath()];
#elif OOLITE_WINDOWS
		// ShellExecute will automatically use the app associated with .log files
		ShellExecute(NULL, NULL, [OOLogHandlerGetLogPath() UTF8String], NULL, NULL, SW_SHOWNORMAL);
#elif  OOLITE_LINUX
		// MKW - needed to suppress 'ignoring return value' warning for system() call
		//		int ret;
		// CIM - and now the compiler complains about that too... casting return
		// value to void seems to keep it quiet for now
		// Nothing to do here, since we dump to stdout instead of to a file.
		//OOLogOutputHandlerStopLoggingToStdout();
		(void) system([[NSString stringWithFormat:@"cat \"%@\"", OOLogHandlerGetLogPath()] UTF8String]);
#else 
		do {} while (0);
#endif
	}
}


#endif	// OO_OXP_VERIFIER_ENABLED
